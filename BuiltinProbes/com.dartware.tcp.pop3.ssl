<!-- 
	POP3 (com.dartware.tcp.pop3.ssl)
	Copyright (c) 2003 Dartware, LLC. All rights reserved.
  Edited description for User Guide (via Probe Doc Generator) 2 Sept 2013 -bobm
  2 Sept 2013 bobm - Updated version number 
-->

<header>
	type			= 	"tcp-script"
	package			= 	"com.dartware"
	probe_name		=	"tcp.pop3.ssl"
	human_name		=	"POP3-SSL"
	version			= 	"2.7"
	address_type	= 	"IP"
	port_number		=	"995"
	display_name	=	"Servers-Standard/Mail/POP3-SSL"

</header>

<description>

\GB\Post Office Protocol Version 3 over SSL (POP3-SSL)\P\

The protocol used to access email messages from a central maildrop server, as defined in \U2=http://www.ietf.org/rfc/rfc1939.txt\RFC 1939\P0\.  The default TCP port number for POP3-SSL connections is port 995.


If the "User Name" parameter is left empty, this probe verifies that the server send "+OK" as its initial greeting, then immediately sends the QUIT command.
\b\Parameters\p\

\i\User Name\p\ - if left empty, the probe verifies that the server sends "+OK" as its initial greeting, then immediately sends the QUIT command. If specified, the probe attempts to use the specified \i\Password\p\ to login to the POP3 server. If the login fails , the device is set to \b\Warning\p\.

\i\Use APOP if supported\p\ - By default, the probe uses the APOP command to authenticate the user if the server supports it. To authenticate via USER and PASS commands for a particular user, set this parameter to \bi\False\p\.

\b\Note:\p\ If APOP is not supported by the server, the \i\Use APOP if supported\p\ option has no effect.

</description>

<parameters>

"User Name"		=	""
"Password*"		=	""
"Use APOP if supported[True,False]"	= 	"True"

</parameters>

<script>

CONN #60 "SECURE"
WAIT #30 @IDLE (idle timeout in secs)
MTCH "+OK"i else goto @UNEXPECTED_GREETING
MTCH m"(<[^>]+>)"i else goto @NO_APOP
STOR "MESSID" "${1}"
SBNE "${Use APOP if supported[True,False]}" "True" @NO_APOP
GOTO @APOP

# Login using APOP command, if a user name is specified.
@APOP:
CHCK "${User Name}" else goto @BYE_OKAY
SEND "APOP ${User Name} ${_MD5:${MESSID}${Password*}}\r\n"
MTCH "+OK"i else goto @APOP_FAIL
GOTO @BYE_OKAY

# Login using USER and PASS commands, if a user name is specified.
@NO_APOP:
CHCK "${User Name}" else goto @BYE_OKAY
SEND "USER ${User Name}\r\n"
MTCH "+OK"i else goto @USER_FAIL
SEND "PASS ${Password*}\r\n"
MTCH "+OK"i else goto @PASS_FAIL
GOTO @BYE_OKAY

@USER_FAIL:
STAT WARN "[POP3] Failed to authenticate using USER command. (${_LINE:50})"
GOTO @BYE

@PASS_FAIL:
STAT WARN "[POP3] Failed to authenticate using PASS command. (${_LINE:50})"
GOTO @BYE

@APOP_FAIL:
STAT WARN "[POP3] Failed to authenticate using APOP command. (${_LINE:50})"
GOTO @BYE

@BYE_OKAY:
STAT OKAY
@BYE:
SEND "QUIT\r\n"
EXPT "+OK" #+1
EXIT

@UNEXPECTED_GREETING:
DONE DOWN "[POP3] Unexpected greeting from port ${_REMOTEPORT}. (${_LINE:50})"

@IDLE:
DONE DOWN "[POP3] No data for ${_IDLETIMEOUT} seconds.  Was expecting \"${_STRINGTOMATCH}\". [Line ${_IDLELINE}]"

</script>
